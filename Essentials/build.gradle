plugins {
    id("essentials.shadow-module")
}

dependencies {
    compileOnly('com.github.milkbowl:VaultAPI:1.7') {
        exclude group: "org.bukkit", module: "bukkit"
    }
    compileOnly 'net.luckperms:api:5.0'

    api 'io.papermc:paperlib:1.0.6'

    api 'org.bstats:bstats-bukkit:2.2.1'

    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation 'org.checkerframework:checker-qual:3.14.0'

    // Providers
    api project(':providers:BaseProviders')
    api project(':providers:PaperProvider')
    api project(':providers:FoliaProvider')
    api(project(':providers:NMSReflectionProvider')) {
        exclude group: "org.bukkit", module: "bukkit"
    }
    api(project(':providers:1_8Provider')) {
        exclude group: "org.spigotmc", module: "spigot"
    }
    api(project(':providers:1_12Provider')) {
        exclude group: "org.bukkit", module: "bukkit"
    }
}

test {
    testLogging.showStandardStreams = true
}

shadowJar {
    dependencies {
        include (dependency('io.papermc:paperlib'))
        include (dependency('org.bstats:bstats-bukkit'))
        include (dependency('org.bstats:bstats-base'))
        include (dependency('org.spongepowered:configurate-yaml'))
        include (dependency('org.spongepowered:configurate-core'))
        include (dependency('org.yaml:snakeyaml'))
        include (dependency('io.leangen.geantyref:geantyref'))
        include (dependency('org.checkerframework:checker-qual'))
        include (project(':providers:BaseProviders'))
        include (project(':providers:PaperProvider'))
        include (project(':providers:FoliaProvider'))
        include (project(':providers:NMSReflectionProvider'))
        include (project(':providers:1_8Provider'))
        include (project(':providers:1_12Provider'))
    }
    relocate 'io.papermc.lib', 'com.earth2me.essentials.paperlib'
    relocate 'org.bstats', 'com.earth2me.essentials.libs.bstats'
    relocate 'org.spongepowered.configurate', 'com.earth2me.essentials.libs.configurate'
    relocate 'org.yaml.snakeyaml', 'com.earth2me.essentials.libs.snakeyaml'
    relocate 'io.leangen.geantyref', 'com.earth2me.essentials.libs.geantyref'
    relocate 'org.checkerframework', 'com.earth2me.essentials.libs.checkerframework'

    minimize {
        include(dependency('org.checkerframework:checker-qual'))
    }
}
